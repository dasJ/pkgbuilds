# Maintainer: Janne He√ü <jannehess@gmail.com>

# Contributor: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: xeross <contact at xeross dot me>
# Contributor: codekoala <codekoala at gmail dot com>

pkgname=etcd
pkgver=2.2.5
pkgrel=1
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url='https://github.com/coreos/etcd'
license=('Apache')
makedepends=('go')
backup=('etc/conf.d/etcd')
install="${pkgname}.install"
source=("git://github.com/coreos/${pkgname}/#tag=v${pkgver}"
        "${pkgname}.service"
        "${pkgname}.conf")
sha512sums=('SKIP'
            '4f1e20b93465a977486371ab73cbc74039ec3ac0394bfb62417bb8b2a8add4d75bc3ad218fa310a91dbd1a7b6d400e1b2f7714622ba54b17963881de573e3a1e'
            '040cee3c04dd5bb253415169d1f6f50bbccac10b687706c1b168184985c0c6c6cc67858f0c71d2ebb475891e54b370fa39b20ead5155658d01fac31d7d388324')

build() {
	cd "${srcdir}/${pkgname}"
	# If we set GIT_DIR, the commit hash will be baked into the executable
	GIT_DIR="${srcdir}/../${pkgname}" ./build
}

check() {
	cd "${srcdir}/${pkgname}"
	./test
}

package() {
	install -Dm755 "${srcdir}/${pkgname}/bin/etcd" "${pkgdir}/usr/bin/etcd"
	install -Dm755 "${srcdir}/${pkgname}/bin/etcdctl" "${pkgdir}/usr/bin/etcdctl"

	install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -Dm644 "${srcdir}/${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
	install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
	install -Dm644 `find "${srcdir}/${pkgname}/Documentation/" -name '*.md'` "${pkgdir}/usr/share/doc/${pkgname}"
}
