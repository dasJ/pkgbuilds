# Maintainer: Janne He√ü <jannehess@gmail.com>

# Contributor: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: xeross <contact at xeross dot me>
# Contributor: codekoala <codekoala at gmail dot com>

pkgname=etcd
pkgver=2.3.1
pkgrel=1
_commit='2b67f52'
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
url='https://github.com/coreos/etcd'
license=('Apache')
makedepends=('go' 'sed' 'findutils')
backup=('etc/conf.d/etcd')
install="${pkgname}.install"
source=("https://github.com/coreos/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}.conf")
sha512sums=('e44553cdd8206a540fff5b87216ec81d49ab9db44b05e6b6cf118f82fcf5b756a509e44626fc8382fa7adb4555cafc75b8120158b9e94ebb622a865dc221e924'
            'd042ebf0cc89102492b89aa57a92e9a2ce3a9865fe703a2d1f4ca5f9d468aea0f4d804d93d9f78697555be1b9e8e0f6090770fec713c9eb04472158c41dea18e')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# Add commit hash into binary
	sed -i "s/^GIT_SHA=.*$/GIT_SHA='${_commit}'/g" build
	# Fix unit file for config file
	sed -i "/ExecStart=/i EnvironmentFile=-/etc/conf.d/etcd" contrib/systemd/etcd.service
}


build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -Dm755 bin/etcd "${pkgdir}/usr/bin/etcd"
	install -Dm755 bin/etcdctl "${pkgdir}/usr/bin/etcdctl"

	install -Dm644 contrib/systemd/etcd.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -Dm644 "${srcdir}/${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
	install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
	install -Dm644 `find Documentation/ -name '*.md'` "${pkgdir}/usr/share/doc/${pkgname}"
}
